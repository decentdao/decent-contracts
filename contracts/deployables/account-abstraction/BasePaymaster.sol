// SPDX-License-Identifier: GPL-3.0
pragma solidity ^0.8.30;

/* solhint-disable reason-string */

import "@openzeppelin/contracts/utils/introspection/IERC165.sol";
import "@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol";
import "@account-abstraction/contracts/interfaces/IPaymaster.sol";
import "@account-abstraction/contracts/interfaces/IEntryPoint.sol";
import "@account-abstraction/contracts/core/UserOperationLib.sol";

/**
 * Helper class for creating a paymaster.
 * provides helper methods for staking.
 * Validates that the postOp is called only by the entryPoint.
 */
abstract contract BasePaymaster is IPaymaster, OwnableUpgradeable {
    /// @custom:storage-location erc7201:Decent.BasePaymaster.main
    struct BasePaymasterStorage {
        IEntryPoint entryPoint;
    }

    // EIP-7201: keccak256(abi.encode(uint256(keccak256("Decent.BasePaymaster.main")) - 1)) & ~bytes32(uint256(0xff))
    bytes32 internal constant BASE_PAYMASTER_STORAGE_LOCATION =
        0xad46a2c487d466a30553d8946911648c5925537fb9ab436a7edd0606d8258100;

    function _getBasePaymasterStorage()
        internal
        pure
        returns (BasePaymasterStorage storage $)
    {
        assembly {
            $.slot := BASE_PAYMASTER_STORAGE_LOCATION
        }
    }

    uint256 internal constant PAYMASTER_VALIDATION_GAS_OFFSET =
        UserOperationLib.PAYMASTER_VALIDATION_GAS_OFFSET;
    uint256 internal constant PAYMASTER_POSTOP_GAS_OFFSET =
        UserOperationLib.PAYMASTER_POSTOP_GAS_OFFSET;
    uint256 internal constant PAYMASTER_DATA_OFFSET =
        UserOperationLib.PAYMASTER_DATA_OFFSET;

    constructor() {
        _disableInitializers();
    }

    function __BasePaymaster_init(
        address _owner,
        IEntryPoint _entryPoint
    ) internal onlyInitializing {
        __Ownable_init(_owner);
        _validateEntryPointInterface(_entryPoint);

        BasePaymasterStorage storage $ = _getBasePaymasterStorage();
        $.entryPoint = _entryPoint;
    }

    //sanity check: make sure this EntryPoint was compiled against the same
    // IEntryPoint of this paymaster
    function _validateEntryPointInterface(
        IEntryPoint _entryPoint
    ) internal virtual {
        require(
            IERC165(address(_entryPoint)).supportsInterface(
                type(IEntryPoint).interfaceId
            ),
            "IEntryPoint interface mismatch"
        );
    }

    /// @inheritdoc IPaymaster
    function validatePaymasterUserOp(
        PackedUserOperation calldata userOp,
        bytes32 userOpHash,
        uint256 maxCost
    ) external override returns (bytes memory context, uint256 validationData) {
        _requireFromEntryPoint();
        return _validatePaymasterUserOp(userOp, userOpHash, maxCost);
    }

    /**
     * Validate a user operation.
     * @param userOp     - The user operation.
     * @param userOpHash - The hash of the user operation.
     * @param maxCost    - The maximum cost of the user operation.
     */
    function _validatePaymasterUserOp(
        PackedUserOperation calldata userOp,
        bytes32 userOpHash,
        uint256 maxCost
    ) internal virtual returns (bytes memory context, uint256 validationData);

    /// @inheritdoc IPaymaster
    function postOp(
        PostOpMode mode,
        bytes calldata context,
        uint256 actualGasCost,
        uint256 actualUserOpFeePerGas
    ) external override {
        _requireFromEntryPoint();
        _postOp(mode, context, actualGasCost, actualUserOpFeePerGas);
    }

    /**
     * Get the entry point.
     */
    function entryPoint() public view returns (IEntryPoint) {
        BasePaymasterStorage storage $ = _getBasePaymasterStorage();
        return $.entryPoint;
    }

    /**
     * Post-operation handler.
     * (verified to be called only through the entryPoint)
     * @dev If subclass returns a non-empty context from validatePaymasterUserOp,
     *      it must also implement this method.
     * @param mode          - Enum with the following options:
     *                        opSucceeded - User operation succeeded.
     *                        opReverted  - User op reverted. The paymaster still has to pay for gas.
     *                        postOpReverted - never passed in a call to postOp().
     * @param context       - The context value returned by validatePaymasterUserOp
     * @param actualGasCost - Actual gas used so far (without this postOp call).
     * @param actualUserOpFeePerGas - the gas price this UserOp pays. This value is based on the UserOp's maxFeePerGas
     *                        and maxPriorityFee (and basefee)
     *                        It is not the same as tx.gasprice, which is what the bundler pays.
     */
    function _postOp(
        PostOpMode mode,
        bytes calldata context,
        uint256 actualGasCost,
        uint256 actualUserOpFeePerGas
    ) internal virtual {
        (mode, context, actualGasCost, actualUserOpFeePerGas); // unused params
        // subclass must override this method if validatePaymasterUserOp returns a context
        revert("must override");
    }

    /**
     * Add a deposit for this paymaster, used for paying for transaction fees.
     */
    function deposit() public payable {
        BasePaymasterStorage storage $ = _getBasePaymasterStorage();
        $.entryPoint.depositTo{value: msg.value}(address(this));
    }

    /**
     * Withdraw value from the deposit.
     * @param withdrawAddress - Target to send to.
     * @param amount          - Amount to withdraw.
     */
    function withdrawTo(
        address payable withdrawAddress,
        uint256 amount
    ) public onlyOwner {
        BasePaymasterStorage storage $ = _getBasePaymasterStorage();
        $.entryPoint.withdrawTo(withdrawAddress, amount);
    }

    /**
     * Add stake for this paymaster.
     * This method can also carry eth value to add to the current stake.
     * @param unstakeDelaySec - The unstake delay for this paymaster. Can only be increased.
     */
    function addStake(uint32 unstakeDelaySec) external payable onlyOwner {
        BasePaymasterStorage storage $ = _getBasePaymasterStorage();
        $.entryPoint.addStake{value: msg.value}(unstakeDelaySec);
    }

    /**
     * Return current paymaster's deposit on the entryPoint.
     */
    function getDeposit() public view returns (uint256) {
        BasePaymasterStorage storage $ = _getBasePaymasterStorage();
        return $.entryPoint.balanceOf(address(this));
    }

    /**
     * Unlock the stake, in order to withdraw it.
     * The paymaster can't serve requests once unlocked, until it calls addStake again
     */
    function unlockStake() external onlyOwner {
        BasePaymasterStorage storage $ = _getBasePaymasterStorage();
        $.entryPoint.unlockStake();
    }

    /**
     * Withdraw the entire paymaster's stake.
     * stake must be unlocked first (and then wait for the unstakeDelay to be over)
     * @param withdrawAddress - The address to send withdrawn value.
     */
    function withdrawStake(address payable withdrawAddress) external onlyOwner {
        BasePaymasterStorage storage $ = _getBasePaymasterStorage();
        $.entryPoint.withdrawStake(withdrawAddress);
    }

    /**
     * Validate the call is made from a valid entrypoint
     */
    function _requireFromEntryPoint() internal virtual {
        BasePaymasterStorage storage $ = _getBasePaymasterStorage();
        require(msg.sender == address($.entryPoint), "Sender not EntryPoint");
    }
}
